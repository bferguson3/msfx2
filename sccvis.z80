 fname "sccplus.bin"

header:
 db $fe
 dw init
 dw EOF
 dw init

 org $c000


init:
DetectSCC:

ENASLT:		EQU	$0024
EXPTBL:		EQU	$FCC1
SCCSLOT: equ $ce34

SCANSLT:
	XOR	A		; start from slot 0
	LD	HL,EXPTBL
.loop:
	LD	C,(HL)
	OR	C
.loop2:
	EXX

	LD	(CHK_SCC.c_slt),A	; save current slot

	PUSH	AF

	LD	H,$80
	CALL	ENASLT

	CALL	CHK_SCC

	POP	AF

	EXX

	ADD	A,%00000100	; increase subslot number
	LD	C,A		; C = new slot + subslot
	BIT	7,A		; check if slot expanded
	JR	Z,.next		; if not expanded go for next slot
	AND	%00001100	; check if already checked all subslots
	LD	A,C
	JR	NZ,.loop2	; still some subslots left
.next:
	INC	HL
	LD	A,C
	INC	A
	AND	%00000011
	RET	Z		; return if all slots & subslots are checked!
;
	JR	.loop

CHK_SCC:
	LD	HL,$A000
	LD	DE,$BFFE
	LD	BC,$2000	; C = 00h -> SCC = YES if found
	XOR	A
	LD	(DE),A		; SET ROM + SCC if SCC-I cartridge
	LD	A,(HL)
	INC	A
	LD	(HL),A
	CP	(HL)
	RET	Z		; found RAM -> return

	LD	HL,$97FF
	LD	(HL),L		; activate SCC write (write xx111111)
	INC	HL		; HL = 9800h
	LD	A,(HL)
	INC	A
	LD	(HL),A
	CP	(HL)
	RET	NZ
	DEC	A
	LD	(HL),A

.f_SCC:
	XOR	A
	DEC	HL
	LD	(HL),A		; disable write to SCC RAM

	LD	A,B
	LD	(DE),A		; SET ROM + SCC+ if SCC-I present
	LD	HL,$B7FF
	LD	(HL),L		; activate SCC+ write (write 1xxxxxxx)
	INC	HL		; HL = B800h
	LD	A,(HL)
	INC	A
	LD	(HL),A
	CP	(HL)
	JR	NZ,.setSCC
	DEC	A
	LD	(HL),A

.f_SCCI:
	INC	C		; C = 1 -> SCC+ = YES

.setSCC:
	XOR	A
	DEC	HL
	LD	(HL),A		; disable write to SCC+ RAM

	LD	A,$00	; register "A" will contain slot/subslot with SCC Cartridge
.c_slt:	EQU	$ - 1	; register "C" will contain "0" for SCC and "1" for SCC+

    ld (SCCSLOT), a 

    pop af 
    pop af 

	ld a, $20
	ld ($bffe), a 

	ld a, $80
	ld ($b000), a 

	ld a, 15
	ld ($b8aa), a 

	ld a, $ff 
	ld ($b8af), a 

	ld a, 3 
	ld ($b8a1), a 
	
	ld hl, waveform
    ld de, $b800
    ld bc, 32
    ldir 

loop: jp loop 

waveform:
 db $58,$51,$41,$32,$1e,$0f,$01,$f6
 db $e7,$e0,$d9,$d2,$cc,$c2,$b9,$ad
 db $a6,$9a,$94,$8a,$83,$80,$80,$80
 db $80,$80,$80,$85,$8a,$92,$9d,$a2
EOF: